plugins {
    id 'application'
    id 'jacoco'
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(
            'com.itextpdf:itext7-core:7.2.0',
            'org.jetbrains.kotlin:kotlin-stdlib:1.6.10'
    )

    testImplementation(
            'org.junit.jupiter:junit-jupiter-api:5.8.2',
            'org.jetbrains.kotlin:kotlin-test-common:1.6.10',
            'org.jetbrains.kotlin:kotlin-test:1.6.10'
    )
    testRuntimeOnly(
            'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    )
}

application {
    mainClassName = 'ktext.Main'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'ktext.Main'
        )
    }

    // magic to copy in all dependencies
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    // stop Exception in thread "main" java.lang.SecurityException: Invalid signature file digest for Manifest main attributes
    exclude('META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA')
    // stop > Entry META-INF/versions/9/module-info.class is a duplicate but no duplicate handling strategy has been set.
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

tasks.withType(JavaCompile).all {
    options.compilerArgs.add("-Xlint:all")
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        outputs.upToDateWhen { false }
        showStandardStreams = true
    }

    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
}

test.finalizedBy jacocoTestReport
